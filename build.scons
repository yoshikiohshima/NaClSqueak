#! -*- python -*-
#
# Copyright (c) 2011 Yoshiki Ohshima.   All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

#Change this flag to compile the VM with embedded image file.  To make Etoys.c, use mkimage utility.
embedded_image_file = False

import make_nacl_env
import os

nacl_env = make_nacl_env.NaClEnvironment()

nacl_env.Append(CPPPATH=['nacl/vm', 'Cross/vm', 'nacl/src/vm', '.']+(map(str, Glob('Cross/plugins/*'))))

nacl_env.Append(CFLAGS=['-DSQUEAK_BUILTIN_PLUGIN'])

sources = ['naclsqueak.c', './nacl/vm/sqNaClWindow.c',
           './nacl/vm/sqUnixMain.c', './nacl/vm/sqNaClSound.c',
           './nacl/vm/sqNaClFile.c', './nacl/vm/sqUnixMemory.c',
           './nacl/vm/sqUnixExternalPrims.c', './nacl/vm/osExports.c',
           './nacl/vm/ringBufferMessaging.c',
           './Cross/vm/sqVirtualMachine.c', './Cross/vm/sqNamedPrims.c',
           'gnu-interp.c']

if (embedded_image_file):
    nacl_env.Append(CFLAGS=['-DEMBEDDED_IMAGE_FILE'])
    sources += ['Etoys.c']

for dir in Glob('nacl/plugins/*'):
    sources += map(str, Glob(str(dir)+'/*.c'))

for dir in Glob('nacl/src/vm/intplugins/*'):
    sources += map(str, Glob(str(dir)+'/*.c'))

for dir in ['Cross/plugins/JPEGReadWriter2Plugin', 'Cross/plugins/SecurityPlugin', 'Cross/plugins/SoundCodecPrims', 'Cross/plugins/SoundPlugin']:
    sources += map(str, Glob(str(dir)+'/*.c'))

nacl_env.AllNaClModules(sources, 'naclsqueak')

gnuify = Builder(action = 'awk -f gnuify < $SOURCE > $TARGET')
nacl_env.Append(BUILDERS = {'Gnuify' : gnuify})

nacl_env.Gnuify('gnu-interp.c', 'nacl/src/vm/interp.c')

#def plugin(file):
#    sp = file.split('/')
#    return '/'.join(sp + [sp[-1]])

#nacl_env.Append(LIBS=map((lambda x: Library(plugin(str(x)), Glob(str(x) + '/*'))), Glob('vm/plugins/*')))
#nacl_env.Append(LIBS=map((lambda x: Library(plugin(str(x)), Glob(str(x) + '/*'))), Glob('src/vm/intplugins/*')))

